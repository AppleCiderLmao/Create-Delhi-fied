architectury {
    common(enabled_platforms.split(","))
}

loom {
    accessWidenerPath = file("src/main/resources/create_delhified.accesswidener")
}

repositories {
    // mavens for Create Fabric and dependencies
    maven { url = "https://mvn.devos.one/releases" } // Porting Lib releases
    maven { url = "https://mvn.devos.one/snapshots" } // Create and several dependencies
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven" } // Forge Config API Port
    maven { url = "https://maven.minecraftforge.net" }
    maven { url = "https://maven.ithundxr.dev/snapshots" }
    maven { name = "CurseMaven"
        url = "https://cursemaven.com" } // Curse Maven

    maven { // Fabric ASM for Porting Lib
        url = "https://jitpack.io/"
        content { includeGroupAndSubgroups("com.github") }
    }

    maven { url = "https://maven.shedaniel.me" } // Cloth Config, REI
    maven { url = "https://maven.blamejared.com" } // JEI

    maven { url = "https://api.modrinth.com/maven" } // LazyDFU
    maven { url = "https://maven.terraformersmc.com/releases" } // Mod Menu, EMI
}

dependencies {
    forge("net.minecraftforge:forge:$minecraft_version-$forge_version")
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation("net.fabricmc:fabric-loader:$fabric_loader_version")
    // Compile against Create Fabric in common
    // beware of differences across platforms!
    // dependencies must also be pulled in (transitively) for all classes to be remapped properly.
    // All dependencies except Flywheel and Registrate are NOT safe to use!
    // Flywheel and Registrate must also be used carefully due to differences across loaders.
    modCompileOnly("dev.architectury:architectury-forge:${rootProject.architectury_api_version}")
    // Fabric support is discontinued until I update to 6.0.7
    // modCompileOnly("com.simibubi.create:create-fabric-$minecraft_version:$create_fabric_version")
    modImplementation("com.simibubi.create:create-$minecraft_version:$create_forge_version:slim") { transitive = false }
    modImplementation("net.createmod.ponder:Ponder-Forge-$minecraft_version:$ponder_forge_version")
    modCompileOnly("dev.engine-room.flywheel:flywheel-forge-api-$minecraft_version:$flywheel_forge_version")
    modRuntimeOnly("dev.engine-room.flywheel:flywheel-forge-$minecraft_version:$flywheel_forge_version")
    modCompileOnly("com.railwayteam.railways:Steam_Rails-forge-1.20.1:1.6.13-alpha-1.20_create-v6+forge-mc1.20.1-build.1579")
    modImplementation("com.tterrag.registrate:Registrate:$registrate_forge_version")
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
    implementation("io.github.llamalad7:mixinextras-forge:0.4.1")
}
